000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID. FileConv.
000300*convert the unsorted Client-Names file of free-format, 
000400*variable length, records into a sorted file of fixed length
000500*records. The sorted file must be sorted into ascending Client-
000600*Name within ascending County-Name. In addition to converting and 
000700*sorting the file, the program must ensure that the county name is
000800*valid and must convert it to a county number. 
000900 
001000 ENVIRONMENT DIVISION.
001100 INPUT-OUTPUT SECTION.
001200 FILE-CONTROL.
001300     SELECT UNSORTED-NAMES ASSIGN TO "CLIENTS.DAT"
001400                  ORGANIZATION IS LINE SEQUENTIAL.
001500 
001600     SELECT WORK-FILE ASSIGN TO "TEMP.DAT".
001700 
001800     SELECT SORTED-NAMES ASSIGN TO "SCLIENTS.DAT"
001900                  ORGANIZATION IS LINE SEQUENTIAL.
002000 
002100     SELECT ERROR-FILE ASSIGN TO "ERROR.DAT"
002200                  ORGANIZATION IS LINE SEQUENTIAL.
002300 
002400 DATA DIVISION.
002500 FILE SECTION.
002600 
002700 FD  UNSORTED-NAMES.
002800 01  UNSORTED-REC           PIC X(99).
002900 
003000 SD  WORK-FILE.
003100 01  WORK-REC.
003200     02 CLIENT-NAME         PIC X(35).
003300     02 CLIENT-ADDRESS      PIC X(60).
003400     02 COUNTY-NO           PIC 99.
003500     02 CLIENT-NO           PIC 9999.
003600 
003700 FD  SORTED-NAMES.
003800 01  SORTED-REC             PIC X(101).
003900 
004000 FD  ERROR-FILE.
004100 01  ERROR-REC              PIC X(99).
004200 
004300 WORKING-STORAGE SECTION.
004400 01  COUNTY-TABLE.
004500     02 COUNTY-VALUES.
004600        03    FILLER        PIC X(11) VALUE "ANTRIM".
004700        03    FILLER        PIC X(11) VALUE "ARMAGH".
004800        03    FILLER        PIC X(11) VALUE "CARLOW".
004900        03    FILLER        PIC X(11) VALUE "CAVAN".
005000        03    FILLER        PIC X(11) VALUE "CLARE".
005100        03    FILLER        PIC X(11) VALUE "CORK".
005200        03    FILLER        PIC X(11) VALUE "DERRY".
005300        03    FILLER        PIC X(11) VALUE "DONEGAL".
005400        03    FILLER        PIC X(11) VALUE "DOWN".
005500        03    FILLER        PIC X(11) VALUE "DUBLIN".
005600        03    FILLER        PIC X(11) VALUE "FERMANAGH".
005700        03    FILLER        PIC X(11) VALUE "GALWAY".
005800        03    FILLER        PIC X(11) VALUE "KERRY".
005900        03    FILLER        PIC X(11) VALUE "KILDARE".
006000        03    FILLER        PIC X(11) VALUE "KILKENNY".        
006100        03    FILLER        PIC X(11) VALUE "LAOIS".
006200        03    FILLER        PIC X(11) VALUE "LEITRIM".
006300        03    FILLER        PIC X(11) VALUE "LIMERICK".
006400        03    FILLER        PIC X(11) VALUE "LONGFORD".
006500        03    FILLER        PIC X(11) VALUE "LOUTH".
006600        03    FILLER        PIC X(11) VALUE "MAYO".
006700        03    FILLER        PIC X(11) VALUE "MEATH".
006800        03    FILLER        PIC X(11) VALUE "MONAGHAN".
006900        03    FILLER        PIC X(11) VALUE "OFFALY".
007000        03    FILLER        PIC X(11) VALUE "ROSCOMMON".
007100        03    FILLER        PIC X(11) VALUE "SLIGO".
007200        03    FILLER        PIC X(11) VALUE "TIPPERARY".
007300        03    FILLER        PIC X(11) VALUE "TYRONE".
007400        03    FILLER        PIC X(11) VALUE "WATERFORD".
007500        03    FILLER        PIC X(11) VALUE "WESTMEATH".
007600        03    FILLER        PIC X(11) VALUE "WEXFORD".
007700        03    FILLER        PIC X(11) VALUE "WICKLOW".
007800
007900     02 FILLER REDEFINES COUNTY-VALUES.
008000        03 COUNTY-NAME OCCURS 32 TIMES
008100                 ASCENDING KEY COUNTY-NAME
008200                 INDEXED BY COUNTY-NUM
008300                 PIC X(11).       
008400 
008500 01  FILLER                 PIC 9 VALUE 0.
008600     88 END-OF-FILE         VALUE 1.
008700 
008800 01  LOWER-CASE             PIC X(26) 
008900         VALUE "abcdefghijklmnopqrstuvwxyz".
009000 
009100 01  UPPER-CASE             PIC X(26)
009200         VALUE "ABCDEFGHIJKLMNOPQRSTUVWXYZ".
009300 
009400 01  HOLD-ITEMS.
009500     02 HOLD-CLIENT         PIC X(35).
009600     02 HOLD-NAME           PIC X(35).
009700     02 HOLD-COUNTY         PIC X(11).
009800     02 HOLD-CLIENT-NO      PIC X(4).
009900     
010000 01  PTR-ITEMS.
010100     02 UNSTR-PTR           PIC 99.
010200        88 END-OF-ADDRESS   VALUE 61.
010300        88 END-OF-NAME      VALUE 36.
010400     02 STR-PTR             PIC 99.
010500     02 NAME-END            PIC 99.
010600 
010700 PROCEDURE DIVISION.
010800 CONVERT-FILE.
010900     SORT WORK-FILE ON ASCENDING COUNTY-NO, CLIENT-NAME
011000         INPUT PROCEDURE IS CONVERT-RECORDS
011100         GIVING SORTED-NAMES.
011200     STOP RUN.
011300 
011400 CONVERT-RECORDS.
011500     OPEN INPUT UNSORTED-NAMES
011600     OPEN OUTPUT ERROR-FILE
011700     READ UNSORTED-NAMES
011800         AT END SET END-OF-FILE TO TRUE
011900     END-READ
012000     PERFORM UNPACK-RECORDS UNTIL END-OF-FILE
012100     CLOSE ERROR-FILE
012200     CLOSE UNSORTED-NAMES.
012300 
012400 UNPACK-RECORDS.
012500     MOVE SPACES TO WORK-REC
012600     MOVE 1 TO UNSTR-PTR.
012700 
012800     UNSTRING UNSORTED-REC 
012900         DELIMITED BY ","
013000         INTO HOLD-CLIENT, CLIENT-ADDRESS, HOLD-CLIENT-NO
013100     MOVE HOLD-CLIENT-NO TO CLIENT-NO.
013200 
013300     PERFORM UNTIL END-OF-ADDRESS
013400         UNSTRING CLIENT-ADDRESS DELIMITED BY ALL SPACES
013500             INTO HOLD-COUNTY
013600             WITH POINTER UNSTR-PTR
013700 
013800     END-PERFORM    
013900     
014000     INSPECT HOLD-COUNTY CONVERTING LOWER-CASE TO UPPER-CASE.
014100     SEARCH ALL COUNTY-NAME
014200         AT END WRITE ERROR-REC FROM UNSORTED-REC
014300         WHEN COUNTY-NAME(COUNTY-NUM) = HOLD-COUNTY
014400             SET COUNTY-NO TO COUNTY-NUM 
014500             PERFORM RESTRUCTURE-NAME
014600             RELEASE WORK-REC 
014700     END-SEARCH
014800     MOVE SPACES TO UNSORTED-REC
014900     READ UNSORTED-NAMES
015000         AT END SET END-OF-FILE TO TRUE
015100     END-READ.
015200     
015300 RESTRUCTURE-NAME.
015400     MOVE 1 TO UNSTR-PTR, STR-PTR.
015500     PERFORM UNTIL END-OF-NAME
015600         MOVE UNSTR-PTR TO NAME-END
015700         UNSTRING HOLD-CLIENT DELIMITED BY ALL SPACES
015800             INTO HOLD-NAME
015900             WITH POINTER UNSTR-PTR
016000     END-PERFORM        
016100 
016200     STRING HOLD-NAME DELIMITED BY SPACES
016300         SPACE DELIMITED BY SIZE 
016400         INTO CLIENT-NAME
016500         WITH POINTER STR-PTR.
016600 
016700     MOVE 1 TO UNSTR-PTR
016800     PERFORM UNTIL UNSTR-PTR >= NAME-END
016900         UNSTRING HOLD-CLIENT DELIMITED BY SPACES
017000             INTO HOLD-NAME
017100             WITH POINTER UNSTR-PTR
017200         STRING HOLD-NAME DELIMITED BY SPACES
017300             SPACE DELIMITED BY SIZE
017400             INTO CLIENT-NAME
017500             WITH POINTER STR-PTR
017600     END-PERFORM. 
017700            